group 'com.cameraforensics'
version '6.2.4.0'

apply plugin: 'java'

subprojects {
    group 'com.cameraforensics'
    version '6.2.4.0'

    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenLocal()
        maven { url "${artifactoryUrl}/libs-release"}
        mavenCentral()
    }

    configurations {
        provided
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }

// custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

// add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    dependencies {
        provided 'org.elasticsearch:elasticsearch:6.2.4'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
    }

    task prepare(type: Zip, dependsOn:build) {
        from {
            [new File("build/libs/${baseName}-${version}.jar"), new File("build/resources/main/plugin-descriptor.properties")]
        }
    }
}