group 'com.cameraforensics'
version '2.4.1.0'

apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_7

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'

    repositories {
        mavenLocal()
        maven { url "${nexusUrl}/content/groups/public/"}
        mavenCentral()
    }


// custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

// add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(
                        url: "${nexusUrl}/content/repositories/releases") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                snapshotRepository(
                        url: "${nexusUrl}/content/repositories/snapshots") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
            }
        }
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
    }

    task prepare(type: Zip, dependsOn:build) {
        from {
            [new File("build/libs/${baseName}-${version}.jar"), new File("build/resources/main/plugin-descriptor.properties")]
        }
    }
}